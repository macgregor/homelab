SONARR_REPLICAS := env_var_or_default("SONARR_REPLICAS", "1")

sonarr-deploy:
    #!/usr/bin/env bash
    set -e
    kubectl apply -f ./media/sonarr/namespace.yml
    kubectl apply -f ./media/sonarr/storage.yml
    kubectl apply -f ./media/sonarr/network.yml
    kubectl apply -f ./media/sonarr/sonarr.yml
    kubectl create secret generic sonarr-api-key \
        -n media --save-config --dry-run=client \
        --from-literal=SONARR_API_KEY="${SONARR_API_KEY}" \
        -o yaml | kubectl apply -f -
    kubectl apply -f ./media/sonarr/cronjob.yml

sonarr-remove:
    #!/usr/bin/env bash
    kubectl delete -f ./media/sonarr/cronjob.yml || true
    kubectl -n media delete secret/sonarr-api-key || true
    kubectl delete -f ./media/sonarr/network.yml || true
    kubectl delete -f ./media/sonarr/sonarr.yml || true
    kubectl delete -f ./media/sonarr/storage.yml || true

sonarr-debug: (_kube-debug "sonarr" "media")
sonarr-logs: (_kube-logs "sonarr" "media")
sonarr-stop: (_kube-stop "media" "deployment/sonarr")
sonarr-start: (_kube-start "media" "deployment/sonarr" SONARR_REPLICAS)
sonarr-restart: (_kube-restart "media" "deployment/sonarr")
sonarr-status: (_kube-status "sonarr" "media")

sonarr-search:
    kubectl -n media create job --from=cronjob/sonarr-missing-search sonarr-missing-search-manual-$(date +%s)
